plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.docling'
version = '2.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api project(':docling-spi')

    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

// Generate ServiceLoader provider-configuration file
tasks.register('generateServiceLoader') {
    def servicesDir = layout.buildDirectory.dir("resources/main/META-INF/services")
    outputs.dir(servicesDir)

    doLast {
        def dir = servicesDir.get().asFile
        dir.mkdirs()

        def serviceFile = new File(dir, "com.docling.spi.JsonSerializer")
        serviceFile.text = 'com.docling.json.jackson.JacksonJsonSerializer\n'
    }
}

processResources.dependsOn generateServiceLoader

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.docling'
            artifactId = 'docling-json-jackson'
            from components.java
        }
    }
}
